# No-IP script automatizado para mikrotik

#--------------- editar estos valores de acuerdo a su configuracion ------------------

# No-IP datos de login
:local noipuser xxxxx@gmail.com
:local noippass mypassword

#Setear hostname del dispositivo que nombramos en la cuenta de noip
:local noiphost yyyyyxxxx.ddns.net

# cambiar nombre de la interfaz afectada (usar la del pppoe )
:local inetinterface pppoeXXXXX

#------------------------------------------------------------------------------------
:global previousIP

:if ([/interface get $inetinterface value-name=running]) do={
   :local currentIP [/ip address get [find interface="$inetinterface" disabled=no] address]

   :for i from=( [:len $currentIP] - 1) to=0 do={
       :if ( [:pick $currentIP $i] = "/") do={ 
           :set currentIP [:pick $currentIP 0 $i]
       } 
   }

   :if ($currentIP != $previousIP) do={
       :log info "No-IP: Current IP $currentIP is not equal to previous IP, update needed"
       :set previousIP $currentIP
       :local url "http://dynupdate.no-ip.com/nic/update\3Fmyip=$currentIP"
       :local noiphostarray
       :set noiphostarray [:toarray $noiphost]
       :foreach host in=$noiphostarray do={
           :log info "No-IP: Sincronizando $host"
           /tool fetch url=($url . "&hostname=$host") user=$noipuser password=$noippass mode=http dst-path=("no-ip_ddns_update-" . $host . ".txt")
           :log info "No-IP: Host $host actualizado con la IP $currentIP"
       }
   }  else={
        }
} else={
   :log info "No-IP: $inetinterface inactiva actualmente,no se haran cambios."
}
